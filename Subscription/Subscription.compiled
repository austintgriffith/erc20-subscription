pragma solidity ^0.4.24;

/*
  Token Subscriptions on the Blockchain

  WIP POC simplified version of  EIP-1337 / ERC-948

  Austin Thomas Griffith - https://austingriffith.com

  Building on previous works:
  https://gist.github.com/androolloyd/0a62ef48887be00a5eff5c17f2be849a
  https://media.consensys.net/subscription-services-on-the-blockchain-erc-948-6ef64b083a36
  https://medium.com/gitcoin/technical-deep-dive-architecture-choices-for-subscriptions-on-the-blockchain-erc948-5fae89cabc7a
  https://github.com/ethereum/EIPs/pull/1337
  https://github.com/ethereum/EIPs/blob/master/EIPS/eip-1077.md
  https://github.com/gnosis/safe-contracts

  Earlier Meta Transaction Demo:
  https://github.com/austintgriffith/bouncer-proxy

  Huge thanks to, as always, to OpenZeppelin for the rad contracts:
*/



/**
 * @title Elliptic curve signature operations
 * @dev Based on https://gist.github.com/axic/5b33912c6f61ae6fd96d6c4a47afde6d
 * TODO Remove this library once solidity supports passing a signature to ecrecover.
 * See https://github.com/ethereum/solidity/issues/864
 */

library ECRecovery {

  /**
   * @dev Recover signer address from a message by using their signature
   * @param _hash bytes32 message, the hash is the signed message. What is recovered is the signer address.
   * @param _signature bytes signature, the signature is generated using web3.eth.sign()
   */
  function recover(bytes32 _hash, bytes _signature)
    internal
    pure
    returns (address)
  {
    bytes32 r;
    bytes32 s;
    uint8 v;

    // Check the signature length
    if (_signature.length != 65) {
      return (address(0));
    }

    // Divide the signature in r, s and v variables
    // ecrecover takes the signature parameters, and the only way to get them
    // currently is to use assembly.
    // solium-disable-next-line security/no-inline-assembly
    assembly {
      r := mload(add(_signature, 32))
      s := mload(add(_signature, 64))
      v := byte(0, mload(add(_signature, 96)))
    }

    // Version of signature should be 27 or 28, but 0 and 1 are also possible versions
    if (v < 27) {
      v += 27;
    }

    // If the version is correct return the signer address
    if (v != 27 && v != 28) {
      return (address(0));
    } else {
      // solium-disable-next-line arg-overflow
      return ecrecover(_hash, v, r, s);
    }
  }

  /**
   * toEthSignedMessageHash
   * @dev prefix a bytes32 value with "\x19Ethereum Signed Message:"
   * and hash the result
   */
  function toEthSignedMessageHash(bytes32 _hash)
    internal
    pure
    returns (bytes32)
  {
    // 32 is the length in bytes of hash,
    // enforced by the type signature above
    return keccak256(
      abi.encodePacked("\x19Ethereum Signed Message:\n32", _hash)
    );
  }
}



/**
 * @title Ownable
 * @dev The Ownable contract has an owner address, and provides basic authorization control
 * functions, this simplifies the implementation of "user permissions".
 */
contract Ownable {
  address public owner;


  event OwnershipRenounced(address indexed previousOwner);
  event OwnershipTransferred(
    address indexed previousOwner,
    address indexed newOwner
  );


  /**
   * @dev The Ownable constructor sets the original `owner` of the contract to the sender
   * account.
   */
  constructor() public {
    owner = msg.sender;
  }

  /**
   * @dev Throws if called by any account other than the owner.
   */
  modifier onlyOwner() {
    require(msg.sender == owner);
    _;
  }

  /**
   * @dev Allows the current owner to relinquish control of the contract.
   * @notice Renouncing to ownership will leave the contract without an owner.
   * It will not be possible to call the functions with the `onlyOwner`
   * modifier anymore.
   */
  function renounceOwnership() public onlyOwner {
    emit OwnershipRenounced(owner);
    owner = address(0);
  }

  /**
   * @dev Allows the current owner to transfer control of the contract to a newOwner.
   * @param _newOwner The address to transfer ownership to.
   */
  function transferOwnership(address _newOwner) public onlyOwner {
    _transferOwnership(_newOwner);
  }

  /**
   * @dev Transfers control of the contract to a newOwner.
   * @param _newOwner The address to transfer ownership to.
   */
  function _transferOwnership(address _newOwner) internal {
    require(_newOwner != address(0));
    emit OwnershipTransferred(owner, _newOwner);
    owner = _newOwner;
  }
}



/**
 * @title ERC20 interface
 * @dev see https://github.com/ethereum/EIPs/issues/20
 */
contract ERC20 {
  function totalSupply() public view returns (uint256);

  function balanceOf(address _who) public view returns (uint256);

  function allowance(address _owner, address _spender)
    public view returns (uint256);

  function transfer(address _to, uint256 _value) public returns (bool);

  function approve(address _spender, uint256 _value)
    public returns (bool);

  function transferFrom(address _from, address _to, uint256 _value)
    public returns (bool);

  event Transfer(
    address indexed from,
    address indexed to,
    uint256 value
  );

  event Approval(
    address indexed owner,
    address indexed spender,
    uint256 value
  );
}


contract Subscription is Ownable{
  using ECRecovery for bytes32;

  constructor() public { }

  //contract will need to hold funds to pay gas
  // copied from https://github.com/uport-project/uport-identity/blob/develop/contracts/Proxy.sol
  function () payable { emit Received(msg.sender, msg.value); }
  event Received (address indexed sender, uint value);

  //similar to a nonce that avoids replay attacks this allows a single execution
  // every x seconds for a given subscription
  //   subscriptionHash  => next valid block number
  mapping(bytes32 => uint) public nextValidTimestamp;

  //for some cases of delegated execution, this contract will pay a third party
  // to execute the transfer. If this happens, the owner of this contract must
  // sign the subscriptionHash
  mapping(bytes32 => bool) public publisherSigned;

  //only the owner of this contract can sign the subscriptionHash to whitelist
  // a specific subscription to start rewarding the relayers for paying the
  // gas of the transactions out of the balance of this contract
  function signSubscriptionHash(bytes32 subscriptionHash) public onlyOwner returns(bool) {
    publisherSigned[subscriptionHash]=true;
    return true;
  }

  //this is used by external smart contracts to verify on-chain that a
  // particular subscription is "paid" and "active"
  // there must be a small grace period added to allow the publisher
  // or desktop miner to execute
  function isSubscriptionActive(bytes32 subscriptionHash,uint gracePeriodSeconds) external view returns (bool) {
    return (block.timestamp >= nextValidTimestamp[subscriptionHash]+gracePeriodSeconds);
  }

  //given the subscription details, generate a hash and try to kind of follow
  // the eip-191 standard and eip-1077 standard from my dude @avsa
  function getSubscriptionHash(
    address from, //the subscriber
    address to, //the publisher
    address tokenAddress, //the token address paid to the publisher
    uint256 tokenAmount, //the token amount paid to the publisher
    uint256 periodSeconds, //the period in seconds between payments
    address gasToken, //the address of the token to pay relayer (0 for eth)
    uint256 gasPrice, //the amount of tokens or eth to pay relayer (0 for free)
    address gasPayer //the address that will pay the tokens to the relayer
  )
     public
     view
     returns (bytes32)
  {
     return keccak256(abi.encodePacked(byte(0x19),byte(0),address(this),from,to,tokenAddress,tokenAmount,periodSeconds,gasToken,gasPrice,gasPayer));
  }

  //ecrecover the signer from hash and the signature
  function getSubscriptionSigner(
    bytes32 subscriptionHash, //hash of subscription
    bytes signature //proof the subscriber signed the meta trasaction
  )
    public
    pure
    returns (address)
  {
    return subscriptionHash.toEthSignedMessageHash().recover(signature);
  }

  //check if a subscription is signed correctly and the timestamp is ready for
  // the next execution to happen
  function isSubscriptionReady(
    address from, //the subscriber
    address to, //the publisher
    address tokenAddress, //the token address paid to the publisher
    uint256 tokenAmount, //the token amount paid to the publisher
    uint256 periodSeconds, //the period in seconds between payments
    address gasToken, //the address of the token to pay relayer (0 for eth)
    uint256 gasPrice, //the amount of tokens or eth to pay relayer (0 for free)
    address gasPayer, //the address that will pay the tokens to the relayer
    bytes signature //proof the subscriber signed the meta trasaction
  )
    public
    view
    returns (bool)
  {
    bytes32 subscriptionHash = getSubscriptionHash(from,to,tokenAddress,tokenAmount,periodSeconds,gasToken,gasPrice,gasPayer);
    address signer = getSubscriptionSigner(subscriptionHash,signature);
    uint allowance = (ERC20(tokenAddress)).allowance(from,address(this));
    return (signer==from && block.timestamp>=nextValidTimestamp[subscriptionHash] && allowance>=tokenAmount);
  }

  //you don't really need this if you are using the approve/transferFrom method
  // because you control the flow of tokens by approving this contract address,
  // but to make the contract an extensible examplefor later user I'll add this
  function cancelSubscription(
    address from, //the subscriber
    address to, //the publisher
    address tokenAddress, //the token address paid to the publisher
    uint256 tokenAmount, //the token amount paid to the publisher
    uint256 periodSeconds, //the period in seconds between payments
    address gasToken, //the address of the token to pay relayer (0 for eth)
    uint256 gasPrice, //the amount of tokens or eth to pay relayer (0 for free)
    address gasPayer, //the address that will pay the tokens to the relayer
    bytes signature //proof the subscriber signed the meta trasaction
  )
  public
  returns (
    bool success
  ){
    bytes32 subscriptionHash = getSubscriptionHash(from,to,tokenAddress,tokenAmount,periodSeconds,gasToken,gasPrice,gasPayer);
    address signer = subscriptionHash.toEthSignedMessageHash().recover(signature);
    //the signature must be valid
    require(signer==from,"Invalid Signature");
    //underflow nextValidTimestamp for this subscriptionHash
    nextValidTimestamp[subscriptionHash]=0;
    nextValidTimestamp[subscriptionHash]=nextValidTimestamp[subscriptionHash]-1;
    //at this point the nextValidTimestamp should be a timestamp that will never
    //be reached during the brief window human existence
    return true;
  }

  //execute the transferFrom to pay the publisher from the subscriber
  // the subscriber has full control by approving this contract an allowance
  function executeSubscription(
    address from, //the subscriber
    address to, //the publisher
    address tokenAddress, //the token address paid to the publisher
    uint256 tokenAmount, //the token amount paid to the publisher
    uint256 periodSeconds, //the period in seconds between payments
    address gasToken, //the address of the token to pay relayer (0 for eth)
    uint256 gasPrice, //the amount of tokens or eth to pay relayer (0 for free)
    address gasPayer, //the address that will pay the tokens to the relayer
    bytes signature //proof the subscriber signed the meta trasaction
  )
  public
  returns (
    bool success
  ){
    //make sure the subscription is valid and ready
    // pulled this out so I have the hash, should be exact code as "isSubscriptionReady"
    bytes32 subscriptionHash = getSubscriptionHash(from,to,tokenAddress,tokenAmount,periodSeconds,gasToken,gasPrice,gasPayer);
    address signer = getSubscriptionSigner(subscriptionHash,signature);

    //the signature must be valid
    require(signer==from,"Invalid Signature");
    //timestamp must be equal to or past the next period
    require(block.timestamp>=nextValidTimestamp[subscriptionHash],"Subscription is not ready");

    //increment the next valid period time
    if(nextValidTimestamp[subscriptionHash]<=0){
      //if this is the very first, start from the current time
      nextValidTimestamp[subscriptionHash]=block.timestamp+periodSeconds;
    }else{
      nextValidTimestamp[subscriptionHash]=nextValidTimestamp[subscriptionHash]+periodSeconds;
    }


    //now, let make the transfer from the subscriber to the publisher
    bool result = (ERC20(tokenAddress)).transferFrom(from,to,tokenAmount);
    if(result) {
      emit ExecuteSubscription(from,to,tokenAddress,tokenAmount,periodSeconds,gasToken,gasPrice,gasPayer);
    }else{
      emit FailedExecuteSubscription(from,to,tokenAddress,tokenAmount,periodSeconds,gasToken,gasPrice,gasPayer);
    }

    //it is possible for the subscription execution to be run by a third party
    // incentivized in the terms of the subscription with a gasToken and gasPrice
    // pay that out now...
    if(gasPrice>0){
      if(gasToken==address(0)){
        //this is an interesting case where the service will pay the third party
        // ethereum out of the subscription contract itself
        // for this to work the publisher must send ethereum to the contract
        require(from==owner || publisherSigned[subscriptionHash],"Publisher has not signed this subscriptionHash");
        require(msg.sender.call.value(gasPrice).gas(36000)(),"Subscription contract failed to pay ether to relayer");
      }else if(gasPayer==address(this)||gasPayer==address(0)){
        //in this case, this contract will pay a token to the relayer to
        // incentivize them to pay the gas for the meta transaction
        // for security, the publisher must have signed the subscriptionHash
        require(from==owner || publisherSigned[subscriptionHash],"Publisher has not signed this subscriptionHash");
        //require((ERC20(gasToken)).transfer(msg.sender,gasPrice),"Failed to pay gas as contract");
        emit DebugBalance((ERC20(gasToken)).balanceOf(address(this)));
        emit DebugBalance(gasPrice);
      //  bool txresult = (ERC20(gasToken)).transfer(msg.sender,gasPrice);
      //  emit Debug(txresult);
      }else if(gasPayer==from){
        //in this case the relayer is paid with a token from the subscriber
        // this works best if it is the same token being transferred to the
        // publisher because it is already in the allowance
        require((ERC20(gasToken)).transferFrom(from,msg.sender,gasPrice),"Failed to pay gas as from account");
      }else{
        //the subscriber could craft the gasPayer to be a fellow subscriber that
        // that has approved this contract to move tokens and then exploit that
        // don't allow that...
        require(false,"The gasPayer is invalid");
        //on the other hand it might be really cool to allow *any* account to
        // pay the third party as long as they have approved this contract
        // AND the publisher has signed off on it. The downside would be a
        // publisher not paying attention and signs a subscription that attacks
        // a different subscriber
      }
    }

    return result;
  }
  event DebugBalance(uint balance);
  event Debug(bool result);

  event ExecuteSubscription(
    address indexed from, //the subscriber
    address indexed to, //the publisher
    address tokenAddress, //the token address paid to the publisher
    uint256 tokenAmount, //the token amount paid to the publisher
    uint256 periodSeconds, //the period in seconds between payments
    address gasToken, //the address of the token to pay relayer (0 for eth)
    uint256 gasPrice, //the amount of tokens or eth to pay relayer (0 for free)
    address gasPayer //the address that will pay the tokens to the relayer
  );
  event FailedExecuteSubscription(
    address indexed from, //the subscriber
    address indexed to, //the publisher
    address tokenAddress, //the token address paid to the publisher
    uint256 tokenAmount, //the token amount paid to the publisher
    uint256 periodSeconds, //the period in seconds between payments
    address gasToken, //the address of the token to pay relayer (0 for eth)
    uint256 gasPrice, //the amount of tokens or eth to pay relayer (0 for free)
    address gasPayer //the address that will pay the tokens to the relayer
  );

}


